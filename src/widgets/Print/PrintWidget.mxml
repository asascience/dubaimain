<?xml version="1.0" encoding="utf-8"?>
<!--
     ////////////////////////////////////////////////////////////////////////////////
     //
     // Copyright (c) 2010 ESRI
     //
     // All rights reserved under the copyright laws of the United States.
     // You may freely redistribute and use this software, with or
     // without modification, provided you include the original copyright
     // and use restrictions.  See use restrictions in the file:
     // <install location>/License.txt
     //
     ////////////////////////////////////////////////////////////////////////////////
-->
<viewer:BaseWidget xmlns:fx="http://ns.adobe.com/mxml/2009"
                   xmlns:s="library://ns.adobe.com/flex/spark"
                   xmlns:mx="library://ns.adobe.com/flex/mx"
                   xmlns:viewer="com.esri.viewer.*"
                   x="600" y="300" contentBackgroundAlpha=".7"
                   widgetConfigLoaded="init()">

    <fx:Style>
        .PrintBox
        {
            color: #000000; /* for the printed page */
			paddingTop: 10;
        }
    </fx:Style>

    <fx:Script>
        <![CDATA[
			import com.esri.viewer.AppEvent;
			import com.esri.viewer.IBaseWidget;
			import com.esri.viewer.ViewerContainer;
			
			import mx.containers.Canvas;
			import mx.containers.VBox;
			import mx.controls.Alert;
			import mx.graphics.ImageSnapshot;
			import mx.printing.FlexPrintJob;
			import mx.printing.FlexPrintJobScaleType;
			
			import org.alivepdf.display.Display;
			import org.alivepdf.display.PageMode;
			import org.alivepdf.layout.*;
			import org.alivepdf.pdf.PDF;
			import org.alivepdf.saving.Method;
			
			import spark.components.BorderContainer;
			import spark.components.Label;
			import spark.components.VGroup;
			import spark.primitives.BitmapImage;

            [Bindable]
            private var title:String;

            [Bindable]
            private var subtitle:String;

            private var copyright:String = "";

            //labels
            [Bindable]
            private var titleLabel:String = "Title:";

            [Bindable]
            private var subtitleLabel:String = "Subtitle:";

            [Bindable]
            private var submitLabel:String = "Print";
			
			[Bindable]
			private var pdfsubmitLabel:String = "Save as PDF";

            private function init():void
            {
				if (configXML)
                {
                    if (configXML.title[0])
                    {
                        title = configXML.title[0];
                    }
                    if (configXML.subtitle[0])
                    {
                        subtitle = configXML.subtitle[0];
                    }
                    if (configXML.copyright[0])
                    {
                        copyright = configXML.copyright[0];
                    }

                    //labels
                    if (configXML.labels.titlelabel[0])
                    {
                        titleLabel = configXML.labels.titlelabel[0];
                    }
                    if (configXML.labels.subtitlelabel[0])
                    {
                        subtitleLabel = configXML.labels.subtitlelabel[0];
                    }
                    if (configXML.labels.submitlabel[0])
                    {
                        submitLabel = configXML.labels.submitlabel[0];
                    }
                }
            }

            private function printMap():void
            {
                var zoomSliderVisibleBeforePrint:Boolean;
				//ViewerContainer.dispatchEvent(new AppEvent(AppEvent.WIDGET_CHANGE_STATE, {id: 6, state:'closed'}));
                if (map.zoomSliderVisible)
                {
                    map.zoomSliderVisible = false;
                    zoomSliderVisibleBeforePrint = true;
                }
                var printJob:FlexPrintJob = new FlexPrintJob();
                printJob.printAsBitmap = false;
                if (printJob.start())
                {
                    var h:Number = printJob.pageHeight;
                    var w:Number = printJob.pageWidth;
                    
					//VGROUP
					var printBox:VGroup = new VGroup();
					var bc:BorderContainer = new BorderContainer();
                    printBox.styleName = "PrintBox";
                    printBox.width = map.width-60;
					
					bc.width = map.width-5;
					bc.setStyle("borderStyle","solid");
					bc.setStyle("borderWeight",8);
					bc.setStyle("cornerRadius",0);
					bc.addElement(printBox);
                    this.addChild(bc); // add now to workaround https://bugs.adobe.com/jira/browse/SDK-26906
                    try
                    {
                        //TITLE
                        var printTitle:Label = new Label();
                        printTitle.text = txtTitle.text;
                        // TODO: make fontsize specified in stylename overwrite this fontSize 
                        printTitle.setStyle("fontSize", 18);
                        printTitle.percentWidth = 100;
                        printBox.addElement(printTitle);
                        //SUBTITLE
                        var printSubtitle:Label = new Label();
                        printSubtitle.text = txtSubtitle.text;
                        // TODO: make fontsize specified in stylename overwrite this fontSize 
                        printSubtitle.setStyle("fontSize", 16);
                        printSubtitle.percentWidth = 100;
                        printBox.addElement(printSubtitle);
                        //MAP
                        var bmpMap:BitmapData = ImageSnapshot.captureBitmapData(map);
                        var printImg:BitmapImage = new BitmapImage();
                        printImg.smooth = true;
                        printImg.source = bmpMap;
						
						printBox.setStyle("paddingLeft", 7);
						printImg.width = map.width-21;
                        printBox.addElement(printImg);
                        //COPYRIGHT
                        var now:Date = new Date();
                        var printCopy:Label = new Label();
                        printCopy.text = copyright + " Printed on " + now.toLocaleString() + ".";
                        // TODO: make fontsize specified in stylename overwrite this fontSize 
                        printCopy.setStyle("fontSize", h / 48);
                        printCopy.percentWidth = 100;
                        printBox.addElement(printCopy);
						//PRINT
						bc.setStyle("paddingRight", 50);
                        printJob.addObject(bc, FlexPrintJobScaleType.SHOW_ALL);
                        printJob.send();
                        bmpMap.dispose();
                    }
                    catch (error:Error)
                    {
                        Alert.show(error.toString(), wTemplate.widgetTitle);
                    }
                    finally
                    {
                        this.removeChild(bc);
                    }
                }
				addScrollbar();
                if (zoomSliderVisibleBeforePrint)
                {
                    map.zoomSliderVisible = true;
                }
            }
			private function addScrollbar():void
			{
				ViewerContainer.dispatchEvent(new AppEvent(AppEvent.WIDGET_RUN,3));
			}
			/* Map Export part*/
			private function mapExport():void
			{
				var exportPDF:PDF=new PDF(Orientation.LANDSCAPE,"Mm",Size.A4);
				var rs:Resize= new Resize("ResizePage","centered");
				exportPDF.setDisplayMode(Display.FULL_PAGE);
				exportPDF.addPage();
				exportPDF.addImage(this.map,rs,0,0,0,0,0,1,true,"PNG",100);
				var bytes:ByteArray=exportPDF.save(Method.LOCAL);
				var f:FileReference=new FileReference();
				var d:Date = new Date();
				
				f.save(bytes,"dubai_map_"+d.date+"_"+String(d.month+1)+"_"+d.fullYear.toString() +".pdf");
			}
			
        ]]>
    </fx:Script>

    <viewer:WidgetTemplate id="wTemplate" enableIcon="false" enableMinimizeButton="true"
                           width="376" height="200"  enableResizeButton="false"
                           minHeight="168"
                           minWidth="290">
        <mx:Form id="frmPrint" 
                 width="100%" height="100%"
                 paddingBottom="0"
                 verticalScrollPolicy="off">
            <mx:FormItem width="100%" label="{titleLabel}">
                <mx:TextInput id="txtTitle" borderSkin="mx.skins.halo.HaloBorder"
                             width="100%"
                             text="{title}"/>
            </mx:FormItem>
            <mx:FormItem width="100%" label="{subtitleLabel}">
                <mx:TextInput id="txtSubtitle" borderSkin="mx.skins.halo.HaloBorder"
                             width="100%"
                             text="{subtitle}"/>
            </mx:FormItem>
            <s:VGroup width="100%" gap="11" horizontalAlign="center">
                <s:Button click="printMap()" label="{submitLabel}"/>
				<s:Label text="(Use landscape option when printing)" color="0xCA0E00" fontSize="11"/>
				<s:Button click="mapExport()" label="{pdfsubmitLabel}"/>
            </s:VGroup>
        </mx:Form>
    </viewer:WidgetTemplate>
</viewer:BaseWidget>
